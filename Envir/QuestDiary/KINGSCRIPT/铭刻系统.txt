;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[@铭刻系统_1]
{
#SAY
_请选择你需要操作装备的种类\\
_<[ 铭刻武器 ]/@铭刻开始(武器铭刻石,1)>     <[ 铭刻衣服 ]/@铭刻开始(衣服铭刻石,0)>     <[ 铭刻头盔 ]/@铭刻开始(头盔铭刻石,4)>\\
_<[ 铭刻项链 ]/@铭刻开始(项链铭刻石,3)>     <[ 铭刻左镯 ]/@铭刻开始(手镯铭刻石,5)>     <[ 铭刻右镯 ]/@铭刻开始(手镯铭刻石,6)>\\
_<[ 铭刻鞋子 ]/@铭刻开始(鞋子铭刻石,10)>     <[ 铭刻左戒 ]/@铭刻开始(戒指铭刻石,7)>     <[ 铭刻右戒 ]/@铭刻开始(戒指铭刻石,8)>\\
_<[ 铭刻勋章 ]/@铭刻开始(勋章铭刻石,2)>     <[ 铭刻马铠 ]/@铭刻开始(马铠铭刻石,12)>\


[@铭刻开始()]
#ACT
MOV 铭刻_部件 %ARG(1)
MOV 铭刻_位置 %ARG(2)

#IF
CHECKITEMINFO %铭刻_位置

#IF
Equal FLD_NAME ""
#ACT
SYSMSG "你选择的位置没有佩戴任何装备。"
BREAK

#IF
#ACT
MOV 铭刻_装备 %FLD_NAME

#IF
#ACT
LoadValue 铭刻_属性 "ParamControl\铭刻参数.ini" [铭刻属性] [%铭刻_装备]

#IF
Large 铭刻_属性 0
#ELSEACT
BREAK
#ELSESAY
你佩戴的{FCOLOR/1}<$OUTPUT(铭刻_装备)>{FCOLOR/12}不在可铭刻的列表中.\\
<关闭/@Exit>

#IF
#ACT
LoadValue 铭刻_类型 "ParamControl\铭刻参数.ini" [铭刻类型] [%铭刻_装备]
#SAY
你目前佩戴的装备为:{FCOLOR/13}<$OUTPUT(铭刻_装备)>{FCOLOR/12}.\
铭刻费用:{FCOLOR/10}<$OUTPUT(铭刻_部件)>5个+20W金币{FCOLOR/12}.\
铭刻后可以带来{FCOLOR/14} <$OUTPUT(铭刻_属性)> {FCOLOR/12}点属性值增益.\
注意:{FCOLOR/1}铭刻属性只能覆盖,不能叠加{FCOLOR/12}.\\

#IF
Equal 铭刻_类型 "1"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>   <[全系列]/@开始铭刻_OK(全系列)>   <[破坏]/@开始铭刻_OK(破坏)>   <[体力]/@开始铭刻_OK(体力)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "2"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>   <[灵魂]/@开始铭刻_OK(灵魂)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "3"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>   <[破坏]/@开始铭刻_OK(破坏)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "4"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>   <[自然]/@开始铭刻_OK(自然)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "5"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>   <[全系列]/@开始铭刻_OK(全系列)>   <[破坏]/@开始铭刻_OK(破坏)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "6"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "7"
#ACT
BREAK
#SAY
<[防御]/@开始铭刻_OK(防御)>   <[魔御]/@开始铭刻_OK(魔御)>   <[体力]/@开始铭刻_OK(体力)>\\
<[返回]/@铭刻系统_1>

#IF
Equal 铭刻_类型 "8"
#ACT
BREAK
#SAY
<[破坏]/@开始铭刻_OK(破坏)>   <[自然]/@开始铭刻_OK(自然)>   <[灵魂]/@开始铭刻_OK(灵魂)>\\
<[返回]/@铭刻系统_1>

[@开始铭刻_OK()]
#ACT
MOV 铭刻_内容 %ARG(1)

#IF
CHECKITEM %铭刻_部件 5
#ELSEACT
BREAK
#ELSESAY
对不起,你的包袱里没有5个{FCOLOR/1}<$OUTPUT(铭刻_部件)>{FCOLOR/12},操作失败.\\
<关闭/@Exit>

#IF
CHECKGOLD 200000
#ELSEACT
BREAK
#ELSESAY
对不起,你的包袱里没有{FCOLOR/1}20W{FCOLOR/12}金币,操作失败.\\
<关闭/@Exit>

#IF
#ACT
FormatStr "where FLD_MAKEINDEX = '%s'" %FLD_MAKEINDEX
Mov 铭刻_索引 %A9
FormatStr "%s,'%s','%s'" %FLD_MAKEINDEX,%铭刻_内容,%铭刻_装备
Mov 铭刻_写入 %A9
ACTCMD WRITESQL "TBL_MingKe" "FLD_MAKEINDEX,FLD_MK,FLD_MKNAME" %铭刻_写入 %铭刻_索引
TAKE 金币 200000
TAKE %铭刻_部件 5
BREAK
#SAY
{FCOLOR/10}装备已经铭刻好了,重新佩戴就可以看到效果了{FCOLOR/12}.\\
<[返回]/@铭刻系统_1>

}